<?xml version="1.0" encoding="utf-8"?>
<!--
/*
    Weave (Web-based Analysis and Visualization Environment)
    Copyright (C) 2008-2011 University of Massachusetts Lowell

    This file is a part of Weave.

    Weave is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License, Version 3,
    as published by the Free Software Foundation.

    Weave is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with Weave.  If not, see <http://www.gnu.org/licenses/>.
*/
-->
<mx:VBox 
	xmlns:mx="http://www.adobe.com/2006/mxml"
	xmlns="weave.editors.*"
	verticalScrollPolicy="off"
	horizontalScrollPolicy="off"
	implements="weave.api.ui.IDataSourceEditor"
	creationPolicy="all" xmlns:ui="weave.ui.*"
	>
	<mx:Label id="editorTitle" fontWeight="bold" text="{_dbfDataSource ? 'Edit' : 'Add'} DBF Data Source"/>
	<ui:Indent label="{lang('Source Name *')}">
		<mx:TextInput id="sourceName" focusOut="keyTypeSelector.addKeyTypeToMenu(sourceName.text)" width="100%"/>
	</ui:Indent>
	<ui:Indent label="{lang('DBF URL *')}">
		<ui:TextInputWithPrompt id="dbfURL" width="100%" prompt="http://www.example.com/example.dbf"/>
		<mx:Button label="{lang('Open file')}" click="openFile(dbfURL, new FileFilter('DBF', '*.dbf'));"/>
	</ui:Indent>
	<ui:Indent label="{lang('SHP URL')}">
		<ui:TextInputWithPrompt id="shpURL" width="100%"  prompt="http://www.example.com/example.shp"/>
		<mx:Button label="{lang('Open file')}" click="openFile(shpURL, new FileFilter('SHP', '*.shp'));"/>
	</ui:Indent>
	<ui:Indent label="{lang('Key Column')}">
		<mx:TextInput id="keyColumnName" width="100%" toolTip="{lang('Enter the name of the column that uniquely identifies each row in the data set')}"/>
	</ui:Indent>
	<ui:Indent label="{lang('Key Type *')}">
		<ui:KeyTypesMenu id="keyTypeSelector" creationComplete="{if(_dbfDataSource)
						 {
						 	(event.target as KeyTypesMenu).addKeyTypeToMenu(_dbfDataSource.keyType.value);
						 	(event.target as KeyTypesMenu).selectedItem = _dbfDataSource.keyType.value;
						 }}"/>
	</ui:Indent>
	<mx:Spacer height="20"/>
	<mx:Label text="{lang('* = required')}"/>
	<mx:Script>
		<![CDATA[
			import mx.controls.Alert;
			
			import weave.api.WeaveAPI;
			import weave.api.core.ILinkableObject;
			import weave.api.data.IDataSource;
			import weave.api.reportError;
			import weave.compiler.StandardLib;
			import weave.data.DataSources.DBFDataSource;
			import weave.ui.BusyIndicator;
			
			private var busyIndicator:BusyIndicator = new BusyIndicator();

			override protected function createChildren():void
			{
				super.createChildren();
				addChild(busyIndicator);
				busyIndicator.visible = false;
				sourceName.text = WeaveAPI.globalHashMap.generateUniqueName("DBFDataSource");
				keyTypeSelector.addKeyTypeToMenu(sourceName.text);
			}
			
			[Bindable] private var _dbfDataSource:DBFDataSource = null;
			public function setTarget(object:ILinkableObject):void
			{
				_dbfDataSource = object as DBFDataSource;
				
				if (!parent)
				{
					callLater(setTarget, arguments);
					return;
				}
				
				if (_dbfDataSource)
				{
					sourceName.text = WeaveAPI.globalHashMap.getName(_dbfDataSource);
					dbfURL.text = _dbfDataSource.dbfUrl.value;
					shpURL.text = _dbfDataSource.shpUrl.value;
					keyColumnName.text = _dbfDataSource.keyColName.value;
					keyTypeSelector.addKeyTypeToMenu(_dbfDataSource.keyType.value);
					keyTypeSelector.selectedKeyType = _dbfDataSource.keyType.value;
					sourceName.editable = false;
					sourceName.toolTip = lang("Source name is not editable once added to the session. If needed, delete the source and add it again with the new name.")
				}

			}
			
			public function hasPendingChanges():Boolean
			{
				return StandardLib.asString(_dbfDataSource.dbfUrl.value) != StandardLib.asString(dbfURL.text)
					|| StandardLib.asString(_dbfDataSource.shpUrl.value) != StandardLib.asString(shpURL.text)
					|| StandardLib.asString(_dbfDataSource.keyColName.value) != StandardLib.asString(keyColumnName.text)
					|| StandardLib.asString(_dbfDataSource.keyType.value) != StandardLib.asString(keyTypeSelector.selectedKeyType);
			}

			public function applyChanges():void
			{
				_dbfDataSource.dbfUrl.value = dbfURL.text;
				_dbfDataSource.shpUrl.value = shpURL.text;
				_dbfDataSource.keyColName.value = keyColumnName.text;
				_dbfDataSource.keyType.value = keyTypeSelector.selectedKeyType;
			}
			
			public function createDataSource():IDataSource
			{
				if (!sourceName.text || !dbfURL.text)
				{
					Alert.show(lang("Please fill all required fields"), lang("Error"));
					return null;
				}
				if (WeaveAPI.globalHashMap.getObject(sourceName.text))
				{
					Alert.show(lang("Source Name already being used. Please give a different name"), lang("Error"));
					return null;
				}
				_dbfDataSource = WeaveAPI.globalHashMap.requestObject(sourceName.text, DBFDataSource, false);
				if (!_dbfDataSource)
				{
					Alert.show(lang("Unexpected problem. Unable to add the data source."));
					return null;
				}
				applyChanges();
				return _dbfDataSource;
			}
			
			private function openFile(target:TextInputWithPrompt, fileFilter:FileFilter):void
			{
				try
				{
					var _fileLoader:FileReference = new FileReference();
					_fileLoader.addEventListener(Event.SELECT, function(event:Event):void{
						busyIndicator.visible = true;
						_fileLoader.load();
						WeaveAPI.ProgressIndicator.addTask(_fileLoader);
					});
					_fileLoader.addEventListener(ProgressEvent.PROGRESS, function(event:ProgressEvent):void{
						WeaveAPI.ProgressIndicator.updateTask(_fileLoader, event.bytesLoaded / event.bytesTotal);
					});
					_fileLoader.addEventListener(Event.COMPLETE, function(event:Event):void{
						target.text = WeaveAPI.URLRequestUtils.saveLocalFile(_fileLoader.name, _fileLoader.data);
						busyIndicator.visible = false;
						WeaveAPI.ProgressIndicator.removeTask(_fileLoader);
					});
					_fileLoader.addEventListener(IOErrorEvent.IO_ERROR, function(event:IOErrorEvent):void{
						busyIndicator.visible = false;
						reportError(event);
					});
					
					_fileLoader.browse([fileFilter]);
				}
				catch (e:Error)
				{
					reportError(e);
				}
			}
		]]>
	</mx:Script>
</mx:VBox>